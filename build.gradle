buildscript {
    ext {
        screamingLibVersion = "2.0.1-SNAPSHOT"
        paperApiVersion = "1.17.1-R0.1-SNAPSHOT"
        spongeApiVersion = "7.3.0"
        minestomVersion = "master-SNAPSHOT"
        junitVersion = "5.8.2"
        jacksonVersion = "2.13.0"
        checkerFrameworkVersion = "3.20.0"
        miniMessageVersion = "4.10.0-SNAPSHOT"
        cloudExtrasVersion = "1.6.1"
        nettyVersion = "4.1.72.Final"
        guavaVersion = "31.0.1-jre"
        mongoDbDriverVersion = "3.12.10"
    }
}

plugins {
    id 'io.freefair.lombok' version '6.3.0' apply false
    id 'maven-publish'
    id 'java-library'
}

allprojects {
    group 'net.lortservers.iris'
    version '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    jar.baseName "${rootProject.name}-${project.name}"
    javadocJar.baseName "${rootProject.name}-${project.name}"
    sourcesJar.baseName "${rootProject.name}-${project.name}"

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.screamingsandals.org/public/'
        }
        maven {
            url 'https://papermc.io/repo/repository/maven-public/'
        }
    }

    dependencies {
        compileOnly "io.papermc.paper:paper-api:$paperApiVersion"
        compileOnly "org.checkerframework:checker-qual:$checkerFrameworkVersion"
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }

    ext {
        slibPlatformsForModule = { String module, String... platforms ->
            platforms.each {
                dependencies.api "org.screamingsandals.lib:" + module + "-" + it + ":$screamingLibVersion"
            }
        }

        slibModulesForPlatform = { String platform, String... modules ->
            modules.each {
                dependencies.api "org.screamingsandals.lib:" + it + "-" + platform + ":$screamingLibVersion"
            }
        }

        slibCompileOnly = { String platform, String... modules ->
            modules.each {
                dependencies.compileOnly "org.screamingsandals.lib:" + it + "-" + platform + ":$screamingLibVersion"
            }
        }
    }

    publishing {
        repositories {
            it.maven({ MavenArtifactRepository repository ->
                if (((String) project.version).contains("SNAPSHOT")) {
                    repository.url = System.getenv("NEXUS_URL_SNAPSHOT")
                } else {
                    repository.url = System.getenv("NEXUS_URL_RELEASE")
                }
                repository.credentials.username = System.getenv("NEXUS_USERNAME")
                repository.credentials.password = System.getenv("NEXUS_PASSWORD")
            })
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}
