buildscript {
    ext {
        screamingLibVersion = "2.0.1-SNAPSHOT"
        paperApiVersion = "1.17.1-R0.1-SNAPSHOT"
        spongeApiVersion = "7.3.0"
        minestomVersion = "master-SNAPSHOT"
        junitVersion = "5.8.1"
        jacksonVersion = "2.13.0"
        checkerFrameworkVersion = "3.18.1"
        lombokVersion = "1.18.20"
        miniMessageVersion = "4.2.0-SNAPSHOT"
        cloudExtrasVersion = "1.5.0"
        nettyVersion = "4.1.68.Final"
        guavaVersion = "31.0.1-jre"
    }
}

plugins {
    id 'java-library'
}

allprojects {
    group 'net.lortservers'
    version '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'

    jar {
        baseName "${rootProject.name}-${project.name}"
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.screamingsandals.org/public/'
        }
        maven {
            url 'https://papermc.io/repo/repository/maven-public/'
        }
    }

    dependencies {
        compileOnly "io.papermc.paper:paper-api:$paperApiVersion"
        compileOnly "org.checkerframework:checker-qual:$checkerFrameworkVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }

    ext {
        slibPlatformsForModule = { String module, String... platforms ->
            platforms.each {
                dependencies.api "org.screamingsandals.lib:" + module + "-" + it + ":$screamingLibVersion"
            }
        }

        slibModulesForPlatform = { String platform, String... modules ->
            modules.each {
                dependencies.api "org.screamingsandals.lib:" + it + "-" + platform + ":$screamingLibVersion"
            }
        }
    }
}